buildscript {
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.2'
    }
}

apply plugin: 'com.android.application'

if( !project.ext.has("STORE_FILE") ) {
    Properties props = new Properties()
    props.load(new FileInputStream("$project.rootDir/signing.properties"))

    props.each { prop ->
        project.ext.set(prop.key, prop.value)
    }
}

android {
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src', 'src-hidden']
            res.srcDirs = ['res']
        }
    }

    compileSdkVersion 23
    buildToolsVersion "24"

    defaultConfig {
        applicationId "com.joinlunar.updater"
        minSdkVersion 22
        targetSdkVersion 22
        versionCode = (rootProject.ext.has("versionCode") && rootProject.ext.get("versionCode") > 0) ? rootProject.ext.versionCode : 99999999
        versionName = (rootProject.version.equals("unspecified")) ? "0.0.0" : rootProject.version
        setProperty("archivesBaseName", "Updater-${versionCode}-${versionName}")
    }

    signingConfigs {
        release {
            storeFile = file(project.ext.get("STORE_FILE"))
            storePassword = project.ext.get("STORE_PASSWORD")
            keyAlias = project.ext.get("KEY_ALIAS")
            keyPassword = project.ext.get("KEY_PASSWORD")
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            signingConfig signingConfigs.release
        }
        release {
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.flags'
        }
    }

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:23.2.0'
    compile 'com.android.support:leanback-v17:23.2.0'
    compile 'com.android.support:support-v4:23.2.0'
    compile 'com.android.volley:volley:1.0.0'
}
